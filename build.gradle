buildscript {
    repositories {
        mavenCentral()
        maven {url "https://plugins.gradle.org/m2/"}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.22"
    }
}

subprojects {
    apply plugin: 'scala'
    apply plugin: 'application'
    apply plugin: "com.github.maiflai.scalatest"

    version = '1.0'
    group 'rpgboss'

    ext {
        appName = "rpgboss"
        gdxVersion = '1.7.2'
        roboVMVersion = '1.12.0'
        mainClass = "rpgboss.editor.RpgDesktop"
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    dependencies {
        compile "org.scala-lang:scala-library:2.11.12"
        // compile 'log4j:log4j:1.2.17'

        testCompile 'org.scalatest:scalatest_2.11:3.0.5'
        testRuntime 'org.pegdown:pegdown:1.4.2'
    }
}

project(":desktop") {
    mainClass = "rpgboss.editor.RpgDesktop"

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

        compile "org.apache.commons:commons-compress:1.9"
        compile "org.scala-lang.modules:scala-swing_2.11:2.0.0-M2"
        compile "com.github.benhutchison:scalaswingcontrib_2.11:1.5"
        compile "net.java.dev.designgridlayout:designgridlayout:1.8"
        compile "net.lingala.zip4j:zip4j:1.3.2"
        compile "com.fifesoft:rsyntaxtextarea:2.5.3"
        compile "net.coobird:thumbnailator:0.4.8"
    }

    tasks.withType(ScalaCompile) {
        configure(scalaCompileOptions.forkOptions) {
            memoryMaximumSize = '1g'
        }
    }
}

project(":core") {
    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        compile "ch.qos.logback:logback-classic:1.0.6"
        compile "com.google.guava:guava:17.0"
        compile "com.typesafe.scala-logging:scala-logging-slf4j_2.11:2.1.2"
        compile "commons-io:commons-io:2.4"
        compile "net.sf.opencsv:opencsv:2.0"
        compile "org.json4s:json4s-native_2.11:3.2.11"
        compile "org.mozilla:rhino:1.7R4"
        compile "org.scalaj:scalaj-http_2.11:1.1.4"
        compile "javax.websocket:javax.websocket-api:1.1"
        compile "org.glassfish.tyrus.bundles:tyrus-standalone-client:1.10"

        testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"

        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }

    test {
        maxParallelForks = 1
    }

    tasks.withType(ScalaCompile) {
        configure(scalaCompileOptions.forkOptions) {
            memoryMaximumSize = '1g'
        }
    }
}